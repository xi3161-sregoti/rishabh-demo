apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  name: k8scontainerresourcelimits
  annotations:
    description: >-
      Requires containers to have memory and CPU limits set and constrains
      limits to be within the specified maximum values.
    category: QoS
spec:
  crd:
    spec:
      names:
        kind: K8sContainerResourceLimits
      validation:
        openAPIV3Schema:
          type: object
          properties:
            requiredResourceTypes:
              type: array
              items:
                type: string
            requiredResources:
              type: array
              items:
                type: string
            resourceLimits:
              type: object
              properties:
                limits:
                  type: object
                  properties:
                    cpu:
                      type: string
                    memory:
                      type: string
                requests:
                  type: object
                  properties:
                    cpu:
                      type: string
                    memory:
                      type: string
  targets:
    - target: admission.k8s.gatekeeper.sh
      rego: |
        package k8scontainerresourcelimits
        import future.keywords.in

        missing(obj, field) = true {
          not obj[field]
        }

        missing(obj, field) = true {
          obj[field] == ""
        }

        canonify_cpu(orig) = new {
          is_number(orig)
          new := orig * 1000
        }

        canonify_cpu(orig) = new {
          not is_number(orig)
          endswith(orig, "m")
          new := to_number(replace(orig, "m", ""))
        }

        canonify_cpu(orig) = new {
          not is_number(orig)
          not endswith(orig, "m")
          re_match("^[0-9]+$", orig)
          new := to_number(orig) * 1000
        }

        # 10 ** 21
        mem_multiple("E") = 1000000000000000000000 { true }

        # 10 ** 18
        mem_multiple("P") = 1000000000000000000 { true }

        # 10 ** 15
        mem_multiple("T") = 1000000000000000 { true }

        # 10 ** 12
        mem_multiple("G") = 1000000000000 { true }

        # 10 ** 9
        mem_multiple("M") = 1000000000 { true }

        # 10 ** 6
        mem_multiple("k") = 1000000 { true }

        # 10 ** 3
        mem_multiple("") = 1000 { true }

        # Kubernetes accepts millibyte precision when it probably shouldn't.
        # https://github.com/kubernetes/kubernetes/issues/28741
        # 10 ** 0
        mem_multiple("m") = 1 { true }

        # 1000 * 2 ** 10
        mem_multiple("Ki") = 1024000 { true }

        # 1000 * 2 ** 20
        mem_multiple("Mi") = 1048576000 { true }

        # 1000 * 2 ** 30
        mem_multiple("Gi") = 1073741824000 { true }

        # 1000 * 2 ** 40
        mem_multiple("Ti") = 1099511627776000 { true }

        # 1000 * 2 ** 50
        mem_multiple("Pi") = 1125899906842624000 { true }

        # 1000 * 2 ** 60
        mem_multiple("Ei") = 1152921504606846976000 { true }

        get_suffix(mem) = suffix {
          not is_string(mem)
          suffix := ""
        }

        get_suffix(mem) = suffix {
          is_string(mem)
          count(mem) > 0
          suffix := substring(mem, count(mem) - 1, -1)
          mem_multiple(suffix)
        }

        get_suffix(mem) = suffix {
          is_string(mem)
          count(mem) > 1
          suffix := substring(mem, count(mem) - 2, -1)
          mem_multiple(suffix)
        }

        get_suffix(mem) = suffix {
          is_string(mem)
          count(mem) > 1
          not mem_multiple(substring(mem, count(mem) - 1, -1))
          not mem_multiple(substring(mem, count(mem) - 2, -1))
          suffix := ""
        }

        get_suffix(mem) = suffix {
          is_string(mem)
          count(mem) == 1
          not mem_multiple(substring(mem, count(mem) - 1, -1))
          suffix := ""
        }

        get_suffix(mem) = suffix {
          is_string(mem)
          count(mem) == 0
          suffix := ""
        }

        canonify_mem(orig) = new {
          is_number(orig)
          new := orig * 1000
        }

        canonify_mem(orig) = new {
          not is_number(orig)
          suffix := get_suffix(orig)
          raw := replace(orig, suffix, "")
          re_match("^[0-9]+$", raw)
          new := to_number(raw) * mem_multiple(suffix)
        }

        # check is resource is defined
        violation[{"msg": msg}] {
          container := filtered_containers[_]
          not container.resources
          msg := sprintf("container %v in %v %v has no resource defined", [container.name, input.review.object.kind, input.review.object.metadata.name])
        }

        violation[{"msg": msg}] {
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          not container.resources[resourceType]
          msg := sprintf("container %v in %v %v has no resources.%v defined", [container.name,input.review.object.kind, input.review.object.metadata.name, resourceType])
        }

        violation[{"msg": msg}] {
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          limitType := input.parameters.requiredResources[_]
          missing(container.resources[resourceType], limitType)
          msg := sprintf("container %v in %v %v has no resources.%v.%v defined", [container.name, input.review.object.kind, input.review.object.metadata.name, resourceType, limitType])
        }

        violation[{"msg": msg}] {
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          limitType := input.parameters.requiredResources[_]
          limitType == "cpu"
          cpu_orig := container.resources[resourceType][limitType]
          not canonify_cpu(cpu_orig)
          msg := sprintf("container %v in %v %v - %v cpu limit %v could not be parsed", [container.name,input.review.object.kind, input.review.object.metadata.name, resourceType, cpu_orig])
        }

        violation[{"msg": msg}] {
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          limitType := input.parameters.requiredResources[_]
          limitType == "memory"
          mem_orig := container.resources[resourceType][limitType]
          not canonify_mem(mem_orig)
          msg := sprintf("container %v in %v %v - %v memory limit %v could not be parsed", [container.name, input.review.object.kind, input.review.object.metadata.name,resourceType, mem_orig])
        }

        # check is resource cpu is defined and under limit
        violation[{"msg": msg}] {
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          limitType := input.parameters.requiredResources[_]
          limitType == "cpu"
          cpu_orig := container.resources[resourceType][limitType]
          cpu := canonify_cpu(cpu_orig)
          max_cpu_orig := input.parameters.resourceLimits[resourceType][limitType]
          max_cpu := canonify_cpu(max_cpu_orig)
          cpu > max_cpu
          msg := sprintf("container %v in %v %v - %v cpu limit %v is higher than the maximum allowed of %v", [container.name, input.review.object.kind, input.review.object.metadata.name, resourceType, cpu_orig, max_cpu_orig])
        }

        # check is resource memory is defined and under limit
        violation[{"msg": msg}] {
          input.parameters.enableMemoryLimit
          container := filtered_containers[_]
          resourceType := input.parameters.requiredResourceTypes[_]
          limitType := input.parameters.requiredResources[_]
          limitType == "memory"
          mem_orig := container.resources[resourceType][limitType]
          mem := canonify_mem(mem_orig)
          max_mem_orig := input.parameters.resourceLimits[resourceType][limitType]
          max_mem := canonify_mem(max_mem_orig)
          mem > max_mem
          msg := sprintf("container %v in %v %v - %v memory limit %v is higher than the maximum allowed of %v", [container.name,input.review.object.kind, input.review.object.metadata.name, resourceType, mem_orig, max_mem_orig])
        }

        # filtered_containers returns containers and initContainers
        filtered_containers[c] {
          input.review.object.kind == "Pod"
          c := input.review.object.spec.containers[_]
        }

        filtered_containers[c] {
          input.review.object.kind == "Pod"
          c := input.review.object.spec.initContainers[_]
        }

        filtered_containers[c] {
          input.review.object.kind in ["Deployment", "StatefulSet", "Job"]
          c := input.review.object.spec.template.spec.containers[_]
        }

        filtered_containers[c] {
          input.review.object.kind in ["Deployment", "StatefulSet", "Job"]
          c := input.review.object.spec.template.spec.initContainers[_]
        }

        filtered_containers[c] {
          input.review.object.kind == "CronJob"
          c := input.review.object.spec.jobTemplate.spec.template.spec.containers[_]
        }

        filtered_containers[c] {
          input.review.object.kind == "CronJob"
          c := input.review.object.spec.jobTemplate.spec.template.spec.initContainers[_]
        }

        
